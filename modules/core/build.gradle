plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

group 'de.deutschebahn.bahnhoflive'

ext.loadDebugStation = { stationNr ->
// from ..\\local.properties
//    dbgConstStation2 = "Berlin Hbf", "1071", "52.525592" , "13.369545", "8011160", "8089021", "8098160"

    Properties properties = new Properties()

    if (rootProject.file("..\\local.properties").exists()) {
        properties.load(rootProject.file("..\\local.properties").newDataInputStream())
    } else {
        println("file '..\\local.properties' not found")
        return ""
    }

    def dbgStation = '"' + properties.getProperty("dbgConstStation" + stationNr, "").replace('"', '') + '"'
//    println(dbgStation)
    return dbgStation
}

ext.loadMapConsentOverride = 'src/java'

def loadMapConsentOverride = { ->
// from ..\\local.properties
//    dbgMapConsentOverride = "true"|"false"

    Properties properties = new Properties()

    if (rootProject.file("..\\local.properties").exists()) {
        properties.load(rootProject.file("..\\local.properties").newDataInputStream())
    } else {
        println("file '..\\local.properties' not found")
        return ""
    }

    def s = properties.getProperty("dbgMapConsentOverride", "false").toString().trim()
    return s.equalsIgnoreCase("\"true\"").toString().toLowerCase()
}

android {
    namespace "de.deutschebahn.bahnhoflive"
    compileSdk 34
    defaultConfig {
        minSdkVersion 21
        targetSdk 34
        multiDexEnabled true

        buildConfigField "String", "RIS_JOURNEYS_BASE_URL", "\"https://apis.deutschebahn.com/db/apis/ris-journeys/v1/\""

        buildConfigField "String", "RIS_STATIONS_API_KEY", "\"ae429e8b34b1366eb37ca466260f92f8\""
        buildConfigField "String", "RIS_STATIONS_CLIENT_ID", "\"be998c9dd20ae7b9440839580644fc47\""

        buildConfigField "String", "FASTA_API_KEY", "\"15d020725ecb768ea41a5925a26bf50e\""
        buildConfigField "String", "FASTA_CLIENT_ID", "\"1c5f5114b79924f8d34d50cb601d4dd6\""

        buildConfigField "String", "PARKING_INFORMATION_BASE_URL", "\"https://apis.deutschebahn.com/db/apis/parking-information/db-bahnpark/v2/\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexKeepProguard file('multidex-config.pro')

            buildConfigField "String", "DBG_STATION_1", "\"\""
            buildConfigField "String", "DBG_STATION_2", "\"\""
            buildConfigField "String", "DBG_STATION_3", "\"\""
            buildConfigField "String", "DBG_STATION_4", "\"\""
            buildConfigField "String", "DBG_STATION_5", "\"\""

            buildConfigField "Boolean", "DBG_MAP_CONSENT_OVERRIDE", "false"
        }

        debug {
            minifyEnabled false

//            buildConfigField "String", "RIS_JOURNEYS_BASE_URL", "\"https://gateway.businesshub.deutschebahn.com/ris-journeys/trial/v1/\""

            buildConfigField "String", "DBG_STATION_1", loadDebugStation(1)
            buildConfigField "String", "DBG_STATION_2", loadDebugStation(2)
            buildConfigField "String", "DBG_STATION_3", loadDebugStation(3)
            buildConfigField "String", "DBG_STATION_4", loadDebugStation(4)
            buildConfigField "String", "DBG_STATION_5", loadDebugStation(5)

            buildConfigField "Boolean", "DBG_MAP_CONSENT_OVERRIDE", loadMapConsentOverride()
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
//        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    kotlin {
        jvmToolchain(17)
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    api "androidx.legacy:legacy-support-v4:1.0.0"
    api 'androidx.appcompat:appcompat:1.6.1'
    implementation "com.google.android.material:material:1.11.0"
    implementation "androidx.vectordrawable:vectordrawable-animated:1.1.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    api 'androidx.multidex:multidex:2.0.1'
    api 'com.android.volley:volley:1.2.1'
    api 'com.google.android.gms:play-services-maps:18.2.0'
    api 'com.google.code.gson:gson:2.10'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.webkit:webkit:1.9.0'

    final lifecycleLibraryVersion = '2.7.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleLibraryVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycleLibraryVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleLibraryVersion"


    api 'io.reactivex.rxjava2:rxjava:2.2.21'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation 'io.sentry:sentry-android:7.1.0'

    implementation 'com.google.firebase:firebase-messaging-ktx:23.4.0'
    implementation 'com.google.firebase:firebase-analytics:21.5.0'

    testApi "junit:junit:4.13.2"
    testApi "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1"
    testApi "io.mockk:mockk:1.13.8"

    implementation 'androidx.core:core-splashscreen:1.0.1'

    testImplementation("org.json:json:20210307")

    //    implementation "androidx.media:media:1.7.0"
//    implementation 'com.jakewharton:disklrucache:2.0.2'

//    final fuelVersion = '2.3.1'
//    api "com.github.kittinunf.fuel:fuel:$fuelVersion"
//    api "com.github.kittinunf.fuel:fuel-android:$fuelVersion"
//    api "com.github.kittinunf.fuel:fuel-coroutines:$fuelVersion"

//    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
}

